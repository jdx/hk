/// Helper for hk step tests
module hk.builtins.test.helpers
import "../../Config.pkl"

/// A "suite" of tests that test core `check` functionality.
/// (This isn't meant to be exhaustive, but covers the basics)
///
/// Example usage:
/// ```pkl
/// step = new Step {
///   ...
///   tests {
///     ...new Check { <ARGS> }.suite
///   }
/// }
class Check {
  /// The name of the file to run the command on.
  /// (This will be rooted in `{{tmp}}`)
  filename: String

  /// The contents of a file which should fail `check`.
  bad: String

  /// The contents of a file which should pass `check`.
  good: String

  /// Extra files to include in the sandbox, useful for config files.
  /// (These will be rooted in `{{tmp}}`)
  extra_files: Mapping

  /// Exit code to expect for `check`'s failure.
  failure_code: Int = 1

  local rooted_extra_files: Mapping<String, String> =
    extra_files.toMap().mapKeys((key, value) -> "{{tmp}}\(key)").toMapping()

  fixed suite: Mapping<String, Config.StepTest> = new Mapping<String, Config.StepTest> {
    ["check bad file"] = new Config.StepTest {
      run = "check"
      write = new Mapping<String, String> {
        ["{{tmp}}/\(filename)"] = bad
        ...rooted_extra_files
      }
      expect = new Config.StepTestExpect {
        code = failure_code
        files = new Mapping<String, String> {
          // NB: The file didn't change
          ["{{tmp}}/\(filename)"] = bad
        }
      }
    }
    ["check good file"] = new Config.StepTest {
      run = "check"
      write = new Mapping<String, String> {
        ["{{tmp}}/\(filename)"] = good
        ...rooted_extra_files
      }
      expect = new Config.StepTestExpect {
        code = 0
        files = new Mapping<String, String> {
          // NB: The file didn't change
          ["{{tmp}}/\(filename)"] = good
        }
      }
    }
  }
}

/// A "suite" of tests that test the core `fix` functionality.
/// (This isn't meant to be exhaustive, but covers the basics)
class Fix {
  /// The name of the file to run the command on.
  /// (This will be rooted in `{{tmp}}`)
  filename: String

  /// The contents of a file which should fail `check` with a fixable error.
  before: String

  /// The contents of the file after it has been fixed (assumed to pass `check`).
  after: String

  /// Extra files to include in the sandbox, useful for config files.
  /// (These will be rooted in `{{tmp}}`)
  extra_files: Mapping

  /// Exit code to expect for `check`'s failure.
  check_failure_code: Int = 1

  local rooted_extra_files: Mapping<String, String> =
    extra_files.toMap().mapKeys((key, value) -> "{{tmp}}\(key)").toMapping()

  fixed suite: Mapping<String, Config.StepTest> = new Mapping<String, Config.StepTest> {
    ...new Check {
      filename = outer.filename
      bad_file = unfixed_file
      good_file = fixed_file
      extra_files = outer.extra_files
      failure_code = check_failure_code
    }.suite
    ["fix unfixed file"] = new Config.StepTest {
      run = "fix"
      write = new Mapping<String, String> {
        ["{{tmp}}/\(filename)"] = before
        ...rooted_extra_files
      }
      expect = new Config.StepTestExpect {
        code = 0 // NB: Fix shouldn't error for fixable issues
        files = new Mapping<String, String> {
          ["{{tmp}}/\(filename)"] = after
        }
      }
    }
    ["fix fixed file"] = new Config.StepTest {
      run = "fix"
      write = new Mapping<String, String> {
        ["{{tmp}}/\(filename)"] = after
        ...rooted_extra_files
      }
      expect = new Config.StepTestExpect {
        code = 0
        files = new Mapping<String, String> {
          ["{{tmp}}/\(filename)"] = after
        }
      }
    }
  }
}
