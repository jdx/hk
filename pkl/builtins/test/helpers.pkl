/// Helper for hk step tests
module hk.builtins.test.helpers
import "../../Config.pkl"

class TestMaker {
  /// The name of the file to run the command on.
  /// (This will be rooted in `{{tmp}}`)
  filename: String = "file.txt"

  /// Extra files to include in the sandbox, useful for config files.
  /// (These will be rooted in `{{tmp}}`)
  extra_files: Mapping<String, String> = new Mapping {}

  /// (Copied to the test)
  before: String?

  local rooted_extra_files: Mapping<String, String> =
    extra_files.toMap().mapKeys((key, value) -> "{{tmp}}/\(key)").toMapping()

  /// Make a config test
  local function makeTest(
    runType: String,
    contentBefore: String,
    contentAfter: String,
    expectedCode: Int,
  ): Config.StepTest = new Config.StepTest {
    local sandboxFilename = "{{tmp}}/\(filename)"
    run = runType
    before = outer.before
    write = new Mapping<String, String> {
      [sandboxFilename] = contentBefore
      ...rooted_extra_files
    }
    expect = new Config.StepTestExpect {
      code = expectedCode
      files = new Mapping<String, String> {
        [sandboxFilename] = contentAfter
      }
    }
  }

  /// Make a "check" test (where `check` should exit zero)
  function checkPass(contents: String): Config.StepTest = makeTest("check", contents, contents, 0)

  /// Make a "check" test (where `check` should exit nonzero)
  function checkFail(contents: String, code: Int): Config.StepTest =
    makeTest("check", contents, contents, code)

  /// Make a "fix" test (where `fix` should exit zero)
  function fixPass(contentsBefore: String, contentsAfter: String): Config.StepTest =
    makeTest("fix", contentsBefore, contentsAfter, 0)

  /// Make a "fix" test (where `fix` should exit nonzero)
  function fixFail(contentsBefore: String, contentsAfter: String, code: Int): Config.StepTest =
    makeTest("fix", contentsBefore, contentsAfter, code)

  /// Make a TestMaker (same as this one) but using the provided filename
  function withFilename(newFilename: String): TestMaker = (this) {
    filename = newFilename
  }
}
